open Ast

let rec string_of_boolexpr = function
    True -> "True"
  | False -> "False"
  | If(e0,e1,e2) -> "If(" ^ (string_of_boolexpr e0) ^ "," ^ (string_of_boolexpr e1) ^ "," ^ (string_of_boolexpr e2) ^ ")"
  | And(e1,e2) -> "And("^(string_of_boolexpr e1) ^ "," ^ (string_of_boolexpr e2) ^ ")"
  | Or(e1,e2) -> "Or("^(string_of_boolexpr e1) ^ "," ^ (string_of_boolexpr e2) ^ ")"
  | Not(e) -> "Not(" ^ (string_of_boolexpr e) ^ ")"

let parse (s : string) : boolExpr =
  let lexbuf = Lexing.from_string s in
  let ast = Parser.prog Lexer.read lexbuf in
  ast


exception NoRuleApplies

let rec trace1 = function
    If(True,e1,_) -> e1
  | If(False,_,e2) -> e2
  | If(b,e1,e2) -> If(trace1 b,e1,e2)
  | And(True,e2) -> e2
  | And(False,_) -> False
  | And(e1,e2) -> And(trace1 e1, e2)
  | Or(True,_) -> True
  | Or(False,e2) -> e2
  | Or(e1,e2) -> Or(trace1 e1, e2)
  | Not True -> False
  | Not False -> True
  | Not e -> Not(trace1 e)
  | _ -> raise NoRuleApplies

let rec trace e = try
    let e' = trace1 e
    in e::(trace e')
  with NoRuleApplies -> [e]


let rec eval = function
    True -> true
  | False -> false
  | If(b,e1,e2) -> (match (eval b) with
        true -> eval e1
      | false -> eval e2)
  | And(e1,e2) -> (match (eval e1) with
        true -> eval e2
      | false -> false )
  | Or(e1,e2) -> (match (eval e1) with
        true -> true
      | false -> eval e2 )
  | Not e -> (match (eval e) with
        true -> false
      | false -> true )
